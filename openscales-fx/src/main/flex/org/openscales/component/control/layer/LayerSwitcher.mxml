<?xml version="1.0" encoding="utf-8"?>
<control:Control xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:control="org.openscales.component.control.*"
	width="100%">

<mx:Script>
	<![CDATA[
		import mx.events.DragEvent;
		import org.openscales.core.events.MapEvent;
		import org.openscales.core.events.LayerEvent;
		import mx.events.IndexChangedEvent;
		import org.openscales.core.layer.Layer;
		import org.openscales.core.Map;

		
		override public function set map(value:Map):void
		{
			super.map = value;	

			if(map != null)
			{
				this.map.addEventListener(LayerEvent.LAYER_ADDED,this.refresh);
				this.map.addEventListener(LayerEvent.LAYER_CHANGED,this.refresh);
				this.map.addEventListener(LayerEvent.LAYER_REMOVED,this.refresh);
			}
			
			this.refresh();
		}
		
	 	public function refresh(event:LayerEvent = null):void
		{
			baseLayerList.dataProvider = this.baseLayers;
			baseLayerList.rowCount = this.baseLayers.length;
			overlayList.dataProvider = this.overlays;
			overlayList.rowCount = this.overlays.length;
		} 
		
		
		public function get baseLayers():Array
		{
			var layers:Array = this.map.layers;
			var baseLayerArray:Array = new Array();;
			
			for(var i:int=0;i<layers.length;i++)
			{
				var layer:Layer = layers[i] as Layer;
				if(layer.isBaseLayer == true)
					baseLayerArray.push(layer);
			}
			
			return baseLayerArray.reverse();
			
		} 
		
		public function get overlays():Array
		{
			var layers:Array = this.map.layers;
			var overlaysArray:Array = new Array();;
			
			for(var k:int=0;k<layers.length;k++)
			{
				var layer:Layer = layers[k] as Layer;
				if(layer.isBaseLayer == false)
					overlaysArray.push(layer);
			}
			
			return overlaysArray.reverse();
		}
		
		public function changeLayerOrder(event:DragEvent):void
		{
			// new zIndex is overlay layer postion + 1 for the base layer
			(overlayList.selectedItem as Layer).zindex = overlayList.calculateDropIndex(event) + 1;
		}
		
	]]>
</mx:Script>

	<mx:VBox width="100%">
		<mx:Text text="Base Layer" width="100%"/>
		<mx:List id="baseLayerList" itemRenderer="org.openscales.component.control.layer.LayerRenderer" labelField="BaseLayer"
			width="100%"/>
	
		<mx:Text text="Overlays" width="100%"/>
		<mx:List id="overlayList" itemRenderer="org.openscales.component.control.layer.LayerRenderer" labelField="Overlays"
			dragEnabled="true" dropEnabled="true" dragMoveEnabled="true" dragDrop="changeLayerOrder(event)"
			width="100%"/>	
	</mx:VBox>
	
</control:Control>

