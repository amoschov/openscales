<?xml version="1.0" encoding="utf-8"?>
<Control xmlns="org.openscales.component.control.*"
	xmlns:mx="http://www.adobe.com/2006/mxml"
	width="200" height="300"
	creationComplete="finalizeTraceInfo();" xmlns:s="library://ns.adobe.com/flex/spark">
	
	<mx:Script>
		<![CDATA[
			import org.openscales.core.events.TraceEvent;
			import org.openscales.core.Map;
			import org.openscales.core.Trace;
			
			[Bindable] private var temp:String;

			[Bindable] public var _clearButton:Boolean = true;
			public function get clearButton():Boolean {
				return _clearButton;
			}
			public function set clearButton(value:Boolean):void {
				_clearButton = value;
				if (clearLogsButton) {
					clearLogsButton.visible = _clearButton;
				}
			}
			
			override public function set map(value:Map):void {
				if(value) {
					super.map = value;
					this.map.addEventListener(TraceEvent.LOG, displayLog);
					this.map.addEventListener(TraceEvent.INFO, displayLog);
					this.map.addEventListener(TraceEvent.WARNING, displayLog);
					this.map.addEventListener(TraceEvent.ERROR, displayLog);
					this.map.addEventListener(TraceEvent.DEBUG, displayLog);
				}
			}
			
			private function finalizeTraceInfo():void {
				clearLogsButton.visible = clearButton;
				clearTrace();
			}
			
			private function clearTrace():void {
				temp = "<b>Output Console :</b>";
			}
				
			private function displayLog(event:TraceEvent):void {
				var textColor:String = "#000066"; // used if event.type is TraceEvent.LOG or TraceEvent.INFO
				if (event.type == TraceEvent.WARNING) {
					textColor = "#FFCC00";
				} else if (event.type == TraceEvent.ERROR) {
					textColor = "#FF0000";
				} else if (event.type == TraceEvent.DEBUG) {
					textColor = "#0066FF";
				}
				temp += "<br/><font color='"+textColor+"' size='9'>"+event.text+"</font><br/>----------";
			}
		]]>
	</mx:Script>
	
	<s:VGroup width="100%" height="100%" horizontalAlign="center">
	
		<s:TextArea id="textarea" width="100%" height="100%"
			verticalScrollPolicy="auto" 
			valueCommit="textarea.verticalCenter = textarea.verticalCenter"
			
			text="{temp}"
			/>
		
		<s:Button id="clearLogsButton" label="Clear logs" click="{clearTrace();}" />
		
	</s:VGroup>
	
</Control>
