<?xml version="1.0" encoding="utf-8"?>
<control:Control xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:control="org.openscales.component.control.*"
	width="146" height="194">
	
	<mx:Script>
		<![CDATA[
			import flash.utils.getQualifiedClassName;
			import org.openscales.core.layer.VectorLayer;
			import org.openscales.core.handler.sketch.DrawPolygonHandler;
			import org.openscales.core.handler.sketch.DrawPathHandler;
			import mx.controls.Alert;
			import org.openscales.core.layer.VectorLayer;
			import org.openscales.core.handler.Handler;
			import org.openscales.core.handler.sketch.DrawPointHandler;
			import org.openscales.core.geometry.Geometry;
			
			import org.openscales.core.Map;
			
			
			public var drawLayer:VectorLayer = new VectorLayer("Sketch");			
			public var pointHandler:DrawPointHandler = new DrawPointHandler(null, false, drawLayer);
			public var polygonHandler:DrawPolygonHandler = new DrawPolygonHandler(null, false, drawLayer);
			public var pathHandler:DrawPathHandler = new DrawPathHandler(null,false,drawLayer);
			
			
			private var drawType:String = "";
			

			public function onDeleteAllClick(event:Event):void {
				drawLayer.destroyFeatures();
				drawLayer.renderer.clear();
				
				switch(drawType)
				{
					case "path":{pathHandler.newFeature = true; break;}
					case "polygon":{polygonHandler.newFeature = true; break;}
				}
			}
			
			public function onDeleteLastClick(event:Event):void {
				var last:Number = drawLayer.features.length - 1;
				
				if (last >= 0) {				
					switch(drawType)
					{
						case "path":		
							drawLayer.removeFeatures(drawLayer.features[last]);
							
							if (last - 1 >= 0 && getQualifiedClassName(drawLayer.features[last-1].geometry) == "org.openscales.core.geometry::LineString") {
								pathHandler.lastPoint = drawLayer.features[last-1].geometry.getLastPoint();
							}
							else {
								pathHandler.newFeature = true;
							}
							break;						
						case "polygon":
							var featuresToRemove:Array = [];
							featuresToRemove.push(drawLayer.features[last]);
							if (last-1 >= 0 && getQualifiedClassName(drawLayer.features[last-1].geometry) == "org.openscales.core.geometry::Point") {
								featuresToRemove.push(drawLayer.features[last-1]);
							}
							drawLayer.removeFeatures(featuresToRemove);
							polygonHandler.newFeature = true;
							
							break;
						default:
							drawLayer.removeFeatures(drawLayer.features[last]);
							break;
					}   
				}
				else {
					Alert.show("There are not features to delete !", "Message");
				}
			}
			public function onPathClick(event:Event):void {			
				if(drawType != "path"){
					drawType = "path";
					pathHandler.active = true;
					
					btnPoint.enabled = false;
					btnPolygon.enabled = false;
				}
				else {
					drawType = "";
					pathHandler.active = false;
					btnPoint.enabled = true;
					btnPolygon.enabled = true;
				}
			}
			
			public function onPolygonClick(event:Event):void {
				if(drawType != "polygon") {
					drawType = "polygon"
					
					polygonHandler.active = true;
					
					btnPath.enabled = false;
					btnPoint.enabled = false;
				}
				else {
					drawType = "";
					polygonHandler.active = false;
					
					btnPath.enabled = true;
					btnPoint.enabled = true;
				}
			}
			
			public function onPointClick(event:Event):void {
				if(drawType != "point") {
					drawType = "point";
					pointHandler.active = true;
					
					btnPath.enabled = false;
					btnPolygon.enabled = false;
				}
				else {
					drawType = "";
					pointHandler.active = false;
					
					btnPath.enabled = true;
					btnPolygon.enabled = true;
				}
			}
			
			override public function set map(value:Map):void
			{
				super.map = value;
				
				if (this.map != null) {
					pointHandler.map = this.map;
					pathHandler.map = this.map;
					polygonHandler.map = this.map
					this.map.addLayer(drawLayer);
				}
				
			}
		]]>
	</mx:Script>
	<mx:Button x="10" y="10" id="btnPoint" icon="@Embed('/org/openscales/component/images/IconeDessinPoint.swf')" click="this.onPointClick(event)" toggle="true" height="30" width="30"/>
	<mx:Button x="10" y="78" label="DeleteAll" id="btnDeleteAll" click="this.onDeleteAllClick(event)" width="104"/>
	<mx:Button x="48" y="10" id="btnPath" icon="@Embed('/org/openscales/component/images/IconeDessinLigne.swf')" click="this.onPathClick(event)" toggle="true" height="30" width="30"/>
	<mx:Button x="84" y="10" id="btnPolygon" icon="@Embed('/org/openscales/component/images/IconeDessinPoly.swf')" click="this.onPolygonClick(event)" toggle="true" height="30" width="30"/>
	<mx:Button x="10" y="48" label="DeleteLast" width="104" id="btnDeleteLast" click="this.onDeleteLastClick(event)"/>
	<mx:Text x="10" y="108" text="Tooltip : to finalize a path or a polygon just double click on map when you have finished it." width="126" height="76" fontStyle="italic"/>
	

</control:Control>