<?xml version="1.0" encoding="utf-8"?>
<control:Control xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:control="org.openscales.component.control.*"
	xmlns:os="http://openscales.org" creationComplete="initTabDrawing();"
	width="330" height="330">
		
	<mx:Script>
		<![CDATA[
			import org.openscales.core.handler.mouse.SelectFeature;
			import mx.events.CloseEvent;
			import org.openscales.core.feature.VectorFeature;
			import org.openscales.core.geometry.Polygon;
			import org.openscales.core.feature.Style;
			import org.openscales.core.feature.Feature;
			import flash.sampler.NewObjectSample;
			import org.openscales.core.handler.mouse.DragHandler;
			import mx.controls.ToggleButtonBar;
			import mx.events.ItemClickEvent;
			import org.openscales.core.events.FeatureEvent;
			import flash.utils.getQualifiedClassName;
			import org.openscales.core.layer.VectorLayer;
			import org.openscales.core.handler.sketch.DrawPolygonHandler;
			import org.openscales.core.handler.sketch.DrawPathHandler;
			import mx.controls.Alert;
			import org.openscales.core.layer.VectorLayer;
			import org.openscales.core.handler.Handler;
			import org.openscales.core.handler.sketch.DrawPointHandler;
			import org.openscales.core.geometry.Geometry;
			
			import org.openscales.core.Map;
			
			private var _drawType:String = "";
			public var drawLayer:VectorLayer = new VectorLayer("Sketch");			
			public var pointHandler:DrawPointHandler = new DrawPointHandler(null, false, drawLayer);
			public var polygonHandler:DrawPolygonHandler = new DrawPolygonHandler(null, false, drawLayer);
			public var pathHandler:DrawPathHandler = new DrawPathHandler(null,false,drawLayer);		
			
			//Params for feature selection
			private var _selectStyle:Style = new Style();
			public var selectFeatures:Array = new Array();  //Array witch keep all selectFeature (with ctrl key)
			private var _n:Number = 0;
				
			//new mouse handler
			public var selectFeatureHandler:SelectFeature;
			public var dragHandler:DragHandler;
			

			public function initTabDrawing():void{
				this.featureInfo.map = this.map;
				//For unselect all button in the toggleBarButton
				this.tgBar.selectedIndex = -1;
				
				//Properties for selectFeatureHandler
				this.selectFeatureHandler = new SelectFeature(this.map,drawLayer,true);
				this.selectFeatureHandler.select = onSelectFeature;
				this.selectFeatureHandler.hover = false;
				 
				//Active select feature handler with the map
				this.map.addHandler(selectFeatureHandler);
				
				//Define the style of a feature when it selected
				this.selectStyle.fillColor = 0xFFD700;
				this.selectStyle.strokeColor = 0xFFD700;
			}
			
			/**
		 	 * Show a warning message when you delete selected feature. User can cancel if it's a mistake
		 	 */
			public function onDeleteSelectedClick(event:Event):void {
				Alert.show("Are you sure ?", "Warning", Alert.YES | Alert.NO, null, alertClickHandler);				
			}
			private function alertClickHandler(event:CloseEvent):void {
                // User click on "YES", he wants to apply changes
                if (event.detail==Alert.YES){
                	deleteSelectedFeatures(); 
                }                  
            }
			
			// when you clik on delete selected feature button
			private function deleteSelectedFeatures():void {
				var feature:org.openscales.core.feature.VectorFeature;
				var i:Number = 0;
				for(i;i<drawLayer.features.length;i++){
					feature = drawLayer.features[i];
					if(feature.selected){
						drawLayer.removeFeatures(drawLayer.features[i]);
						i--;
					}
				}
				drawLayer.renderer.clear();
				drawLayer.redraw();
				btnDeleteSelected.enabled = false;
			}

			//When you click on delete all button
			public function onDeleteAllClick(event:Event):void {
				drawLayer.destroyFeatures();
				drawLayer.renderer.clear();
				selectFeatureHandler.lastfeature = null;
				
				switch(drawType)
				{
					case "path":{pathHandler.newFeature = true; break;}
					case "polygon":{polygonHandler.newFeature = true; break;}
				}
			}
			
			//When you click on delete last feature button
			public function onDeleteLastClick(event:Event):void {
				var last:Number = drawLayer.features.length - 1;
				var lastFeature:VectorFeature = null;
				
				if (last >= 0) {					
					lastFeature = drawLayer.features[last];
					drawLayer.removeFeatures(drawLayer.features[last]);
								
					switch(drawType)
					{
						case "path":		
													
							if (last - 1 >= 0 && getQualifiedClassName(drawLayer.features[last-1].geometry) == "org.openscales.core.geometry::LineString") {
								pathHandler.lastPoint = drawLayer.features[last-1].geometry.getLastPoint();
							}
							else {pathHandler.newFeature = true;}
							break;	
												
						case "polygon":

							if (last-1 >= 0 && !polygonHandler.firstPointRemoved && !polygonHandler.newFeature) {
								drawLayer.removeFeatures(drawLayer.features[last - 1]);
							}
							polygonHandler.newFeature = true;
							
							break;
					}   
				}
				else {
					Alert.show("There are not features to delete !", "Message");
					selectFeatureHandler.lastfeature = null;
				}
			}
			
			public function onSelectFeature(event:FeatureEvent):void {			
				if(!pathHandler.active && !polygonHandler.active && !pointHandler.active && featureInfo != null) {					
					var i:Number = 0;
					var f:org.openscales.core.feature.VectorFeature;					
					selectFeatureHandler.currentfeature = event.vectorfeature;
					btnDeleteSelected.enabled = true;							
					//It's not the first selection
					if(selectFeatureHandler.lastfeature != null){		
						//Feature is not already selected
						if(!selectFeatureHandler.currentfeature.selected){
							//ctrl key isn't pressed
							if(!event.ctrlPressed){
								for each(f in selectFeatures){
									if(f != null){
										if(f.selected){f.style = f.originalStyle;f.selected=false;f.layer.redraw();}
									}							
								}
								n=0;
								selectFeatureHandler.currentfeature.originalStyle=selectFeatureHandler.currentfeature.style;					
								selectFeatureHandler.currentfeature.style = selectStyle;							 
								selectFeatureHandler.currentfeature.selected = true;
								selectFeatures[n]=selectFeatureHandler.currentfeature;
								selectFeatureHandler.currentfeature.layer.redraw();
								selectFeatureHandler.lastfeature = selectFeatureHandler.currentfeature;	
								this.featureInfo.showInfo(event);		//Show information of the selected feature
							}
							//ctrl key is pressed
							else{
								selectFeatureHandler.currentfeature.originalStyle=selectFeatureHandler.currentfeature.style;							
								selectFeatureHandler.currentfeature.style = selectStyle;							 
								selectFeatureHandler.currentfeature.selected = true;
								n++;selectFeatures[n]=selectFeatureHandler.currentfeature;
								selectFeatureHandler.currentfeature.layer.redraw();	
							}
						}
						//Feature is already selected
						else{
							//ctrl key isn't pressed
							if(!event.ctrlPressed){
								selectFeatureHandler.currentfeature.style = selectFeatureHandler.currentfeature.originalStyle;												 
								selectFeatureHandler.currentfeature.selected = false;
								selectFeatures[n]=null;	
								selectFeatureHandler.lastfeature = null;
								selectFeatureHandler.currentfeature.layer.redraw();	
								
								this.featureInfo.showInfo(event);					//Show information of the selected feature
								btnDeleteSelected.enabled = false;			
								this.featureInfo.dgFeatureInfo.dataProvider=null;   //clear the information tab
							}
							//ctrl key is pressed
							else{
								selectFeatureHandler.currentfeature.style = selectFeatureHandler.currentfeature.originalStyle;												 
								selectFeatureHandler.currentfeature.selected = false;
								for(i;i<selectFeatures.length;i++){
									f=selectFeatures[i];
									if(f == selectFeatureHandler.currentfeature){selectFeatures[i]=null;}
								}
								selectFeatureHandler.currentfeature.layer.redraw();
							}
						}
					}
					//This is the first selection
					else{
						selectFeatureHandler.currentfeature.originalStyle=selectFeatureHandler.currentfeature.style;					
						selectFeatureHandler.currentfeature.style = selectStyle;							 
						selectFeatureHandler.currentfeature.selected = true;
						selectFeatures[n]=selectFeatureHandler.currentfeature;						
						selectFeatureHandler.currentfeature.layer.redraw();
						selectFeatureHandler.lastfeature = selectFeatureHandler.currentfeature;	
						this.featureInfo.showInfo(event);		//Show information of the selected feature
					}
				}
			}
			
			override public function set map(value:Map):void {
				super.map = value;
				
				if (this.map != null) {
					pointHandler.map = this.map;
					pathHandler.map = this.map;
					polygonHandler.map = this.map
					this.map.addLayer(drawLayer);
				}
			}
			
			private function clickHandler(event:ItemClickEvent):void{
				var handler:Number = event.index;		// handler is a number because of toggleButtonBar
				this.deactiveDrag(); 				
				switch(handler)
				{
					case 0:		//click on button point
						if(drawType!="point"){
							if(pathHandler.active){this.finishPath();}
							else if(polygonHandler.active){this.finishPolygon();}												
							pointHandler.active = true;
							drawType = "point";
						}
						else{
							drawType = "";
							pointHandler.active = false;
							this.tgBar.selectedIndex = -1;
							this.activeDrag(); 
						}						
						break;
					
					case 1:		//click on button path
						if(drawType!="path"){
							if(polygonHandler.active){this.finishPolygon();}						
							pointHandler.active = false;
							pathHandler.active = true;
							drawType = "path";
						}
						else{
							drawType = "";
							finishPath();
							this.tgBar.selectedIndex = -1;
							this.activeDrag(); 
						}
						break;
						
					case 2:		//click on button polygon
						if(drawType!="polygon"){
							if(pathHandler.active){this.finishPath();}						
							pointHandler.active = false;
							polygonHandler.active = true;
							drawType = "polygon";
						}
						else{
							drawType = "";
							this.finishPolygon();
							this.tgBar.selectedIndex = -1;
							this.activeDrag();   
						}
						break;
				}
			}
			
			public function finishPolygon():void{
				polygonHandler.drawFinalPoly();
				polygonHandler.active = false;
			}
			
			public function finishPath():void{
				pathHandler.drawFinalPath();
				pathHandler.active = false;
			}
			
			public function activeDrag():void{
				var handler:Handler;
				for each(handler in this.map.handlers){
					if(handler is DragHandler){
						handler.active = true;
					}
				} 
			}
			public function deactiveDrag():void{
				var handler:Handler;
				for each(handler in this.map.handlers){
					if(handler is DragHandler){
						handler.active = false;
					} 
				}
			}
			
			// Getters and setters AS3
			public function get drawType():String{
				return _drawType;
			}
			public function set drawType(value:String):void{
				_drawType = value;
			}
			
			public function get selectStyle():Style{
				return _selectStyle;
			}
			public function set selectStyle(newStyle:Style):void{
				_selectStyle = newStyle;
			}
			
			public function get n():Number{
				return _n;
			}
			public function set n(value:Number):void{
				_n = value;
			}
		]]>
	</mx:Script>
	
	<mx:ToggleButtonBar x="10" y="10" horizontalGap="5" itemClick="clickHandler(event);" horizontalAlign="center" selectedIndex="-1" id="tgBar">
		<mx:Array>
			<mx:Object icon="@Embed('/org/openscales/component/images/IconeDessinPoint.swf')" id="btnPoint"/>
			<mx:Object icon="@Embed('/org/openscales/component/images/IconeDessinLigne.swf')" id="btnPath"/>
			<mx:Object icon="@Embed('/org/openscales/component/images/IconeDessinPoly.swf')" id="btnPolygon"/>
		</mx:Array>
	</mx:ToggleButtonBar>	
	
	<mx:Button x="10" y="78" label="DeleteAll" id="btnDeleteAll" click="this.onDeleteAllClick(event)" width="104"/>
	<mx:Button x="10" y="48" label="DeleteLast" width="104" id="btnDeleteLast" click="this.onDeleteLastClick(event)"/>
	<mx:Button label="DeleteSelected" width="118" id="btnDeleteSelected" click="this.onDeleteSelectedClick(event)" x="175" y="78" enabled="false"/>
		
	<mx:Text x="160" y="5" text="Tooltip : to finalize a path or a polygon just double click on map." width="143" height="46" fontStyle="italic"/>
	<mx:Text x="160" y="108" text="For the moment, you can't delete WFS points, even if they are selected." fontStyle="italic" width="143" textAlign="center"/>

	<control:FeatureInfo id="featureInfo" x="10" y="155" width="310" height="165"/>
	
</control:Control>