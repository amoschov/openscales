<?xml version="1.0" encoding="utf-8"?>
<control:Control xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:control="org.openscales.component.control.*"
	xmlns:os="http://openscales.org" creationComplete="initTabDrawing();"
	width="330" height="330">
		
	<mx:Script>
		<![CDATA[
			import org.openscales.core.events.SpriteCursorEvent;
			import org.openscales.core.Trace;
			import mx.containers.TabNavigator;
			import mx.events.IndexChangedEvent;
			import org.openscales.core.handler.mouse.DragFeature;
			import mx.effects.easing.Elastic;
			import org.openscales.core.handler.mouse.SelectFeature;
			import mx.events.CloseEvent;
			import org.openscales.core.feature.VectorFeature;
			import org.openscales.core.geometry.Polygon;
			import org.openscales.core.feature.Style;
			import org.openscales.core.feature.Feature;
			import flash.sampler.NewObjectSample;
			import org.openscales.core.handler.mouse.DragHandler;
			import mx.controls.ToggleButtonBar;
			import mx.events.ItemClickEvent;
			import org.openscales.core.events.FeatureEvent;
			import flash.utils.getQualifiedClassName;
			import org.openscales.core.layer.VectorLayer;
			import org.openscales.core.handler.sketch.DrawPolygonHandler;
			import org.openscales.core.handler.sketch.DrawPathHandler;
			import mx.controls.Alert;
			import org.openscales.core.layer.VectorLayer;
			import org.openscales.core.handler.Handler;
			import org.openscales.core.handler.sketch.DrawPointHandler;
			import org.openscales.core.geometry.Geometry;
			
			import org.openscales.core.Map;
			
			private var _drawType:String = "";
			private var _indexTabDrawing:Number = -1;
			public var drawLayer:VectorLayer = new VectorLayer("Drawings");			
			public var pointHandler:DrawPointHandler = new DrawPointHandler(null, false, drawLayer);
			public var polygonHandler:DrawPolygonHandler = new DrawPolygonHandler(null, false, drawLayer);
			public var pathHandler:DrawPathHandler = new DrawPathHandler(null,false,drawLayer);
			
			/**
			 * Params for feature selection
			 */
			private var _selectStyle:Style = new Style();
			public var selectFeatures:Array = new Array();  //Array witch keep all selectFeature (with ctrl key)
			private var _iteratorFeatures:Number = 0;	//indicate the iterate for feature
				
			/**
			 * Mouse handlers
			 */
			public var selectFeatureHandler:SelectFeature; //to select features
			public var dragHandler:DragHandler;		//to pan on the map
			public var dragfeature:DragFeature;		//to drag features

			/**
			 * Initialisation of map and params
			 */
			public function initTabDrawing():void{
				
				this.featureInfo.map = this.map;
				//To unselect all button in the toggleBarButton
				this.tgBar.selectedIndex = -1;
				
				//Properties of selectFeatureHandler
				this.selectFeatureHandler = new SelectFeature(this.map,drawLayer,true);
				this.selectFeatureHandler.select = onSelectFeature;
				this.selectFeatureHandler.hover = false;
				 				 
				//Active drag feature
				this.dragfeature = new DragFeature(this.map,[drawLayer],true);
				
				//Active select feature handler with the map
				this.map.addHandler(selectFeatureHandler);
				
				//Define the style of a feature when it selected
				this.selectStyle.fillColor = 0xFFD700;
				this.selectStyle.strokeColor = 0xFFD700;			
				
				//Manage switching tab
				this.parent.addEventListener(Event.CHANGE,changingTab);
				indexTabDrawing = (this.parent as TabNavigator).selectedIndex;
				
				this.map.dispatchEvent(new SpriteCursorEvent(SpriteCursorEvent.SPRITECURSOR_SHOW_HAND));
			}
			
			/**
		 	 * Show a warning message when you delete selected feature. User can cancel if it's a mistake
		 	 */
			public function onDeleteSelectedClick(event:Event):void {
				Alert.show("Are you sure ?", "Warning", Alert.YES | Alert.NO, null, alertClickHandler);				
			}
			
			//check the user answer
			private function alertClickHandler(event:CloseEvent):void {
                // User click on "YES", he wants to apply changes
                if (event.detail==Alert.YES){
                	deleteSelectedFeatures(); 
                }                  
            }
			
			/**
			 * User clicks on delete selected feature button
			 */
			private function deleteSelectedFeatures():void {
				var feature:org.openscales.core.feature.VectorFeature;
				var i:Number = 0;
				for(i;i<drawLayer.features.length;i++){
					feature = drawLayer.features[i];
					if(feature.selected){
						drawLayer.removeFeatures(drawLayer.features[i]);
						i--;
					}
				}
				drawLayer.renderer.clear();
				drawLayer.redraw();
				btnDeleteSelected.enabled = false;
				this.featureInfo.dgFeatureInfo.dataProvider=null;
			}

			/**
			 * User clicks on delete all button
			 */
			private function onDeleteAllClick(event:Event):void {
				drawLayer.destroyFeatures();
				drawLayer.renderer.clear();
				selectFeatureHandler.lastfeature = null;				
				switch(drawType)
				{
					case "path":{pathHandler.newFeature = true; break;}
					case "polygon":{polygonHandler.newFeature = true; break;}
				}
			}
			
			/**
			 * User clicks on delete last feature button
			 */
			public function onDeleteLastClick(event:Event):void {
				var last:Number = drawLayer.features.length - 1;
				var lastFeature:VectorFeature = null;
				
				if (last >= 0) {					
					lastFeature = drawLayer.features[last];
					drawLayer.removeFeatures(drawLayer.features[last]);
								
					switch(drawType)
					{
						case "path":														
							if (last - 1 >= 0 && getQualifiedClassName(drawLayer.features[last-1].geometry) == "org.openscales.core.geometry::LineString") {
								pathHandler.lastPoint = drawLayer.features[last-1].geometry.getLastPoint();
							}
							else {pathHandler.newFeature = true;}
							break;	
												
						case "polygon":
							if (last-1 >= 0 && !polygonHandler.firstPointRemoved && !polygonHandler.newFeature) {
								drawLayer.removeFeatures(drawLayer.features[last - 1]);
							}
							polygonHandler.newFeature = true;							
							break;
					}   
				}
				else {
					Alert.show("There are not features to delete !", "Message");
					selectFeatureHandler.lastfeature = null;
				}
			}
			
			/**
			 * On select feature
			 */
			private function onSelectFeature(event:FeatureEvent):void {			
				if(!pathHandler.active && !polygonHandler.active && !pointHandler.active && featureInfo != null) {					
					var i:Number = 0;	// to iterate
					var f:org.openscales.core.feature.VectorFeature;					
					selectFeatureHandler.currentfeature = event.vectorfeature;
					btnDeleteSelected.enabled = true;
					if(selectFeatures == null){selectFeatures = new Array();}							
					//It's not the first selection
					if(selectFeatureHandler.lastfeature != null){		
						//Feature is not already selected
						if(!selectFeatureHandler.currentfeature.selected){
							//ctrl key isn't pressed
							if(!event.ctrlPressed){
								for each(f in selectFeatures){
									if(f != null){
										if(f.selected){f.style = f.originalStyle;f.selected=false;f.layer.redraw();}
									}							
								}
								iteratorFeatures=0;
								selectFeatureHandler.currentfeature.originalStyle=selectFeatureHandler.currentfeature.style;					
								selectFeatureHandler.currentfeature.style = selectStyle;							 
								selectFeatureHandler.currentfeature.selected = true;
								selectFeatures[iteratorFeatures]=selectFeatureHandler.currentfeature;
								selectFeatureHandler.currentfeature.layer.redraw();
								selectFeatureHandler.lastfeature = selectFeatureHandler.currentfeature;	
								this.featureInfo.showInfo(event);		//Show information of the selected feature
							}
							//ctrl key is pressed
							else{
								selectFeatureHandler.currentfeature.originalStyle=selectFeatureHandler.currentfeature.style;							
								selectFeatureHandler.currentfeature.style = selectStyle;							 
								selectFeatureHandler.currentfeature.selected = true;
								iteratorFeatures++;selectFeatures[iteratorFeatures]=selectFeatureHandler.currentfeature;
								selectFeatureHandler.currentfeature.layer.redraw();	
								this.featureInfo.showInfo(event);		//Show information of the selected feature
							}
						}
						//Feature is already selected
						else{
							//ctrl key isn't pressed
							if(!event.ctrlPressed){
								var others:Boolean = false;
								for(i=0;i<=selectFeatures.length;i++){
									f=selectFeatures[i];
									if(f!=null && f.selected && f!=selectFeatureHandler.currentfeature){others=true;f.selected = false;f.style = f.originalStyle;f.layer.redraw();selectFeatures[i]=null;}
								}			
								if(!others){
									selectFeatureHandler.currentfeature.style = selectFeatureHandler.currentfeature.originalStyle;												 
									selectFeatureHandler.currentfeature.selected = false;
									selectFeatures[iteratorFeatures]=null;	
									selectFeatureHandler.lastfeature = null;
									selectFeatureHandler.currentfeature.layer.redraw();	
								
									this.featureInfo.showInfo(event);					//Show information of the selected feature
									btnDeleteSelected.enabled = false;			
									this.featureInfo.dgFeatureInfo.dataProvider=null;   //clear the information tab						
								}
							}							
							//ctrl key is pressed
							else{
								selectFeatureHandler.currentfeature.style = selectFeatureHandler.currentfeature.originalStyle;												 
								selectFeatureHandler.currentfeature.selected = false;
								for(i;i<selectFeatures.length;i++){
									f=selectFeatures[i];
									if(f == selectFeatureHandler.currentfeature){selectFeatures[i]=null;}
								}
								selectFeatureHandler.currentfeature.layer.redraw();
							}							
						}
					}
					//This is the first selection
					else{
						selectFeatureHandler.currentfeature.originalStyle=selectFeatureHandler.currentfeature.style;					
						selectFeatureHandler.currentfeature.style = selectStyle;							 
						selectFeatureHandler.currentfeature.selected = true;
						selectFeatures[iteratorFeatures]=selectFeatureHandler.currentfeature;						
						selectFeatureHandler.currentfeature.layer.redraw();
						selectFeatureHandler.lastfeature = selectFeatureHandler.currentfeature;	
						this.featureInfo.showInfo(event);		//Show information of the selected feature
					}
				}
			}
			
			override public function set map(value:Map):void {
				super.map = value;
				
				if (this.map != null) {
					pointHandler.map = this.map;
					pathHandler.map = this.map;
					polygonHandler.map = this.map
					this.map.addLayer(drawLayer);
				}
			}
			
			/**
			 * Click on the togglebarbutton. Index button starts from 0.
			 */ 		
			private function clickHandler(event:ItemClickEvent):void{
				var handler:Number = event.index;		// handler is a number because of toggleButtonBar
				this.deactiveDrag();			
				switch(handler)
				{
					case 0:		//click on button point
						if(drawType!="point"){
							if(pathHandler.active){this.finishPath();}
							else if(polygonHandler.active){this.finishPolygon();}												
							pointHandler.active = true;
							drawType = "point";
						}
						else{
							drawType = "";
							pointHandler.active = false;
							this.tgBar.selectedIndex = -1;
							this.activeDrag(); 
						}						
						break;
					
					case 1:		//click on button path
						if(drawType!="path"){
							if(polygonHandler.active){this.finishPolygon();}						
							pointHandler.active = false;
							pathHandler.active = true;
							drawType = "path";
						}
						else{
							drawType = "";
							finishPath();
							this.tgBar.selectedIndex = -1;
							this.activeDrag(); 
						}
						break;
						
					case 2:		//click on button polygon
						if(drawType!="polygon"){
							if(pathHandler.active){this.finishPath();}						
							pointHandler.active = false;
							polygonHandler.active = true;
							drawType = "polygon";
						}
						else{
							drawType = "";
							this.finishPolygon();
							this.tgBar.selectedIndex = -1;
							this.activeDrag();   
						}
						break;
				}
			}
			/**
			 * Manage switching tab
			 * To prevent a feature selection. If user isn't on drawing tab, he can't select a feature
			 */
			private function changingTab(event:Event):void{
				var f:org.openscales.core.feature.VectorFeature;
				var selectedFeature:Boolean = false;
				
				//User is on Drawing Tab, and wants to switch	
				if((event.target as TabNavigator).selectedIndex != indexTabDrawing){						
					//Check if some feature are selected
					for each(f in selectFeatures){
						if(f != null){if(f.selected){selectedFeature=true;}}
					}				
					//if one (or more) feature is selected, we warn user
					if(selectedFeature){
						Alert.okLabel = "Continue";
						Alert.buttonWidth=200;
						Alert.show("One (or more) feature is selected. If you continue, all selection will be lost.", "Warning", Alert.OK | Alert.CANCEL, null, alertContinueHandler);
					}
					//no feature is selected
					else{
						finishPath(); 
						finishPolygon(); 
						pointHandler.active = false;
						tgBar.selectedIndex = -1;
						drawType = "";
						activeDrag();
						selectFeatureHandler.active = false;
					}
					this.map.dispatchEvent(new SpriteCursorEvent(SpriteCursorEvent.SPRITECURSOR_HIDE_HAND));	
				}
				//user switches to drawing tab
				else{
					selectFeatureHandler.active = true;
					this.map.dispatchEvent(new SpriteCursorEvent(SpriteCursorEvent.SPRITECURSOR_SHOW_HAND));
				}
			}		
			
			//check if the user wants to continue
			private function alertContinueHandler(event:CloseEvent):void {
            	var i:Number = 0;
            	var f:org.openscales.core.feature.VectorFeature;
            	// User click "Continue", he wants to continue
            	if (event.detail==Alert.OK){
               		if(drawType != ""){
						finishPath(); 
						finishPolygon(); 
						pointHandler.active = false;
						tgBar.selectedIndex = -1;
						drawType = "";
						activeDrag();
					}
					else if(selectFeatureHandler != null){
						selectFeatureHandler.active=false;
					}
					for(i;i<selectFeatures.length;i++){
							f=selectFeatures[i];
							if(f != null){if(f.selected){f.selected = false;f.style = f.originalStyle;f.layer.redraw();}}
						}	
						selectFeatures = null;
            	}
            	else{(this.parent as TabNavigator).selectedIndex = indexTabDrawing;}
			}
			
			/**
			 * Finish a polygon
			 * Force to finalyse a polygon (even it's not finished)
			 */
			public function finishPolygon():void{
				polygonHandler.drawFinalPoly();
				polygonHandler.active = false;
			}
			
			/**
			 * Finish a path
			 * Force to finalyse a path (event it's not finished)
			 */
			public function finishPath():void{
				pathHandler.drawFinalPath();
				pathHandler.active = false;
			}
			
			/**
			 * Active draging
			 * User can drag the map
			 */
			public function activeDrag():void{
				var handler:Handler;
				for each(handler in this.map.handlers){
					if(handler is DragHandler){
						handler.active = true;
					}
				} 
			}
			/**
			 * Deactive draging
			 * User can't drag map anymore
			 */
			public function deactiveDrag():void{
				var handler:Handler;
				for each(handler in this.map.handlers){
					if(handler is DragHandler){
						handler.active = false;
					} 
				}
			}
			
			// Getters and setters AS3
			public function get drawType():String{
				return _drawType;
			}
			public function set drawType(value:String):void{
				_drawType = value;
			}
			
			public function get selectStyle():Style{
				return _selectStyle;
			}
			public function set selectStyle(newStyle:Style):void{
				_selectStyle = newStyle;
			}
			
			public function get iteratorFeatures():Number{
				return _iteratorFeatures;
			}
			public function set iteratorFeatures(value:Number):void{
				_iteratorFeatures = value;
			}
			
			public function get indexTabDrawing():Number{
				return _indexTabDrawing;
			}
			public function set indexTabDrawing(value:Number):void{
				_indexTabDrawing = value;
			}
		]]>
	</mx:Script>
	
	<mx:ToggleButtonBar x="10" y="10" horizontalGap="5" itemClick="clickHandler(event);" horizontalAlign="center" selectedIndex="-1" id="tgBar">
		<mx:Array>
			<mx:Object icon="@Embed('/org/openscales/component/images/IconeDessinPoint.swf')" id="btnPoint"/>
			<mx:Object icon="@Embed('/org/openscales/component/images/IconeDessinLigne.swf')" id="btnPath"/>
			<mx:Object icon="@Embed('/org/openscales/component/images/IconeDessinPoly.swf')" id="btnPolygon"/>
		</mx:Array>
	</mx:ToggleButtonBar>	
	
	<mx:Button x="10" y="78" label="DeleteAll" id="btnDeleteAll" click="this.onDeleteAllClick(event)" width="104"/>
	<mx:Button x="10" y="48" label="DeleteLast" width="104" id="btnDeleteLast" click="this.onDeleteLastClick(event)"/>
	<mx:Button label="DeleteSelected" width="118" id="btnDeleteSelected" click="this.onDeleteSelectedClick(event)" x="175" y="78" enabled="false"/>
		
	<mx:Text x="160" y="5" text="Tooltip : to finalize a path or a polygon just double click on map." width="143" height="46" fontStyle="italic"/>
	<mx:Text x="160" y="108" text="For the moment, you can't delete WFS points, even if they are selected." fontStyle="italic" width="143" textAlign="center"/>

	<control:FeatureInfo id="featureInfo" x="10" y="155" width="310" height="165"/>
	
</control:Control>
