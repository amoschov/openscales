<?xml version="1.0" encoding="utf-8"?>
<control:Control xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:control="org.openscales.component.control.*" implements="org.openscales.core.control.IControl">
	
	<mx:Script>
		<![CDATA[
			import org.openscales.core.layer.ogc.WFS;
			import mx.controls.Text;
			import mx.controls.Alert;
			import org.openscales.core.basetypes.maps.HashMap;
			import org.openscales.core.layer.Layer;
			import org.openscales.core.layer.capabilities.GetCapabilities;
			import org.openscales.core.Map;
			
			private var listDataProvider:Array = null;
			private var capabilities:HashMap = null;
			private var featureCap:HashMap = null;
			private var addedLayers:HashMap = new HashMap();
			private var url:String = null;
						
			public function onButtonClick(event:MouseEvent):void{
				var getCap:GetCapabilities = new GetCapabilities("wfs", this.txtUrl.text,this.capabilitiesGetter);
				url = this.txtUrl.text;
			}
			
			public function onChangeListElement(event:Event):void {
				// We show to user the informations about the layer
				featureCap = capabilities.getValue(this.list.selectedItem);
				var info:String = "\tInformation about the layer: \n\n";
				info += "Title: " + featureCap.getValue("Title") + "\n";
				info += "Description: " + featureCap.getValue("Abstract") + "\n";
				info += "SRS: " + featureCap.getValue("SRS");
				
				this.txtInfo.text = info;
			}
			
			public function onDblClickListElement(event:MouseEvent):void {

				//We add the layer to the map
				if (!addedLayers.containsKey(this.list.selectedItem)) {
					var params:Object = new Object();
					params.typename = this.list.selectedItem;
					params.SRS = capabilities.getValue(this.list.selectedItem).getValue("SRS");
					params.VERSION = "1.0.0";
					var newLayer:WFS = new WFS(this.list.selectedItem as String,this.url,params,null,capabilities.getValue(this.list.selectedItem as String));
					newLayer.isBaseLayer = false;
					newLayer.minZoomLevel = this.map.numZoomLevels;
					
					this.map.addLayer(newLayer);
					addedLayers.put(this.list.selectedItem,null);
				}
				else {
					Alert.show("This layer is already added", "Message");
				}
				
				
			}
			
			public function capabilitiesGetter(getCap:GetCapabilities):void {
				capabilities = getCap.getAllCapabilities();
				
				var lb:Label = new Label();
				lb.text = "There are " + capabilities.size() + " features available on server:"
				lb.x = 20; lb.y = 35;
				this.addChild(lb);				
				
				this.list.dataProvider = capabilities.getKeys().sort();	
						
			}
			
			override public function set map(value:Map):void
			{
				super.map = value;
			}
		]]>
	</mx:Script>
	<mx:Label x="10" y="10" text="WFS server URL:" id="lbUrl" enabled="false"/>
	<mx:TextInput x="116" y="8" width="217" id="txtUrl" text="http://sigma.openplans.org/geoserver/wfs" displayAsPassword="false" editable="true" enabled="true"/>
	<mx:Button x="341" y="8" label="Go!" width="49" id="butGo" enabled="true" click="this.onButtonClick(event)"/>
	<mx:List x="10" y="60" width="380" height="114" allowMultipleSelection="false" id="list" doubleClickEnabled="true" doubleClick="this.onDblClickListElement(event)" change="this.onChangeListElement(event)"></mx:List>
	<mx:Text x="10" y="182" width="380" height="108" id="txtInfo" visible="true"/>

</control:Control>