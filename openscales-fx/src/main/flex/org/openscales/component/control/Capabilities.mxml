<?xml version="1.0" encoding="utf-8"?>
<control:Control xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:control="org.openscales.component.control.*" creationComplete="this.map.addEventListener(LayerEvent.LAYER_REMOVED,deleteLayer);" >
	
	<mx:Script>
		<![CDATA[
			import org.openscales.core.events.LayerEvent;
			import org.openscales.core.layer.ogc.WFS;
			import mx.controls.Text;
			import mx.controls.Alert;
			import org.openscales.core.basetypes.maps.HashMap;
			import org.openscales.core.layer.Layer;
			import org.openscales.core.layer.capabilities.GetCapabilities;
			import org.openscales.core.Map;
			
			private var listDataProvider:Array = null;
			private var capabilities:HashMap = null;
			private var featureCap:HashMap = null;
			private var addedLayers:HashMap = new HashMap();
			private var url:String = null;
						
			public function deleteLayer(event:LayerEvent):void
			{
				addedLayers.remove((event.layer as Layer).name);
			}
					
			public function onButtonClick(event:MouseEvent):void{
				var getCap:GetCapabilities = new GetCapabilities("wfs", this.txtUrl.text,this.capabilitiesGetter);
				url = this.txtUrl.text;
			}
			
			public function onChangeListElement(event:Event):void {
				// We show to user the informations about the layer
				featureCap = capabilities.getValue(this.list.selectedItem);
				var info:String = "\tInformation about the layer: \n\n";
				info += "Title: " + featureCap.getValue("Title") + "\n";
				info += "Description: " + featureCap.getValue("Abstract") + "\n";
				info += "SRS: " + featureCap.getValue("SRS");
				
				this.txtInfo.text = info;
			}
			
			public function onDblClickListElement(event:MouseEvent):void {

				//We add the layer to the map
				if (!addedLayers.containsKey(this.list.selectedItem)) {
					var params:Object = new Object();
					params.typename = this.list.selectedItem;
					params.SRS = capabilities.getValue(this.list.selectedItem).getValue("SRS");
					params.VERSION = "1.0.0";
					var newLayer:WFS = new WFS(this.list.selectedItem as String,this.url,params,null,capabilities.getValue(this.list.selectedItem as String));
					newLayer.isBaseLayer = false;
					newLayer.minZoomLevel = this.map.numZoomLevels;
					
					this.map.addLayer(newLayer);
					addedLayers.put(this.list.selectedItem,null);
				}
				else {
					Alert.show("This layer is already added", "Message");
				}
				
				
			}
			
			public function capabilitiesGetter(getCap:GetCapabilities):void {
				this.capabilities = getCap.getAllCapabilities();
				lblFeatures.text = "There are " + capabilities.size() + " features available on server:";
				this.list.dataProvider = capabilities.getKeys().sort();	
			}
			
		]]>
	</mx:Script>
	
	<mx:VBox width="100%">
		<mx:Label text="WFS server URL:" />
		<mx:Label id="lblFeatures" enabled="false"/>
		<mx:HBox width="100%">
			<mx:TextInput width="100%" id="txtUrl" text="http://sigma.openplans.org/geoserver/wfs" displayAsPassword="false" editable="true" enabled="true"/>
			<mx:Button label="Go!" id="butGo" enabled="true" click="this.onButtonClick(event)"/>	
		</mx:HBox>
		
		<mx:List width="100%" height="114" allowMultipleSelection="false" id="list" doubleClickEnabled="true" doubleClick="this.onDblClickListElement(event)" change="this.onChangeListElement(event)"></mx:List>
		<mx:Text width="100%" height="108" id="txtInfo" visible="true"/>
	
	</mx:VBox>
	
</control:Control>